plugins {
    id 'java'
    id 'java-library'
}

version "$metricsVersion"


configurations {
    all.collect { configuration ->
        configuration.exclude group: 'com.amazonaws.aws-java-sdk-core'
        configuration.exclude group: 'com.fasterxml.jackson.core:jackson-annotations'
        configuration.exclude group: 'com.fasterxml.jackson.core:jackson-databind'
        configuration.exclude group: 'org.mock-server'
        configuration.exclude group: 'software.amazon.awssdk:netty-nio-client'
        configuration.exclude group: 'io.netty'
    }

}


repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {

    implementation('gov.cms.ab2d:ab2d-events-client:1.11.2') {
        // Gradle is including these transient dependencies in the zip making it huge
        exclude group: 'org.springframework'
        exclude group: 'org.springframework.boot'
        exclude group: 'com.slack.api'
        exclude group: 'org.jetbrains.kotlin'
        exclude group: 'org.postgresql'
        exclude group: 'com.squareup.okhttp3'
        exclude group: 'commons-codec'
        exclude group: 'com.squareup.okio'
        exclude group: 'org.springframework.cloud'
        exclude group: 'org.testcontainers:testcontainers'
        // Lombok is amazing but its jar is 2 MBs which is 20% of the total fat jar.
        // Even when set as compile and annotationProcessor only gradle still adds it to the fat jar
        exclude group: 'org.projectlombok'
    }
    implementation 'com.amazonaws:aws-lambda-java-core:1.2.2'
    implementation 'com.amazonaws:aws-lambda-java-events:3.11.0'
    implementation 'com.amazonaws:aws-java-sdk-sqs:1.12.342'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-joda:2.14.0'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.14.0'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    compileOnly 'org.projectlombok:lombok:1.18.24'
    implementation 'org.slf4j:slf4j-api:2.0.4'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
    testImplementation 'org.junit.platform:junit-platform-commons:1.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
    testImplementation 'org.mockito:mockito-core:4.8.0'
    testImplementation project(':lambda-test-utils')
}

test {
    useJUnitPlatform()
}
task buildZip(type: Zip) {

    from compileJava
    from processResources
    into('lib') {
        from configurations.runtimeClasspath
    }
    archiveFileName = 'metrics-lambda.zip'
}

task wrapper(type: Wrapper) {
    gradleVersion = '7.5'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

build.dependsOn buildZip

