
plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version '3.2.8'
    id 'com.jfrog.artifactory' version '4.32.0' apply false
    id "org.sonarqube" version "4.1.0.3113"
    id "gov.cms.ab2d.plugin" version "1.0.2"
    id 'maven-publish'
    id 'org.cyclonedx.bom' version '2.0.0' apply true
}

version="2.0.0"
group = "gov.cms.ab2d"

ext {
    springBootVersion= '3.5.7'
    springCloudAwsVersion = '3.4.0'
    liquibaseVersion='5.0.0'
    jacksonVersion='2.20.0'
    testContainerVersion='1.20.6'
    eventClientVersion='3.3.8'
    lombokVersion = '1.18.38'
    commonsLangVersion='3.18.0' // Spring Boot is on 3.17.0; force version upgrade; Remove this property next time Spring Boot is updated
}

allprojects {
    apply plugin: 'gov.cms.ab2d.plugin'
    apply plugin: "org.cyclonedx.bom"
    apply plugin: 'org.springframework.boot'
    dependencies {
        implementation "com.squareup.okhttp3:okhttp:4.12.0"
        implementation "org.springframework.boot:spring-boot-starter:${springBootVersion}"
        implementation "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"
        implementation "org.springframework.boot:spring-boot-starter-jdbc:${springBootVersion}"
        implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
        implementation "io.awspring.cloud:spring-cloud-aws:${springCloudAwsVersion}"
        implementation "io.awspring.cloud:spring-cloud-aws-dependencies:${springCloudAwsVersion}"
        implementation "io.awspring.cloud:spring-cloud-aws-starter-sqs:${springCloudAwsVersion}"
        implementation "io.awspring.cloud:spring-cloud-aws-starter:${springCloudAwsVersion}"
        implementation "org.apache.commons:commons-lang3:${commonsLangVersion}"

        implementation "commons-codec:commons-codec:1.18.0"
        implementation "org.liquibase:liquibase-core:${liquibaseVersion}"
        implementation "com.amazonaws:amazon-kinesis-client:1.15.3"
        implementation "com.fasterxml.jackson.module:jackson-module-parameter-names:${jacksonVersion}"
        implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${jacksonVersion}"
        implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"
        implementation "gov.cms.ab2d:ab2d-events-client:${eventClientVersion}"
        implementation 'com.slack.api:slack-api-client:1.29.2'
        runtimeOnly "javax.xml.bind:jaxb-api:2.4.0-b180830.0359"
        runtimeOnly 'org.postgresql:postgresql:42.7.3'
        testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
        testRuntimeOnly "org.junit.platform:junit-platform-launcher"
        testImplementation "commons-io:commons-io:2.18.0"
        testImplementation "org.springframework.data:spring-data-jpa"
        testImplementation "org.liquibase:liquibase-core:${liquibaseVersion}"
        testImplementation "org.testcontainers:localstack:${testContainerVersion}"
        testImplementation "org.projectlombok:lombok:${lombokVersion}"
        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        implementation(platform(annotationProcessor("org.projectlombok:lombok:${lombokVersion}")))
        annotationProcessor("org.projectlombok:lombok:${lombokVersion}")
    }

    cyclonedxBom {
        // includeConfigs is the list of configuration names to include when generating the BOM (leave empty to include every configuration)
        includeConfigs = ["runtimeClasspath"]
        // skipConfigs is a list of configuration names to exclude when generating the BOM
        skipConfigs = ["compileClasspath", "testCompileClasspath"]
        // Specified the type of project being built. Defaults to 'library'
        projectType = "library"
        // Specified the version of the CycloneDX specification to use. Defaults to 1.4.
        schemaVersion = "1.4"
        // Boms destination directory (defaults to build/reports)
        destination = file("build/reports")
        // The file name for the generated BOMs (before the file format suffix). Defaults to 'bom'
        outputName = "bom"
        // The file format generated, can be xml, json or all for generating both
        outputFormat = "all"
        // Exclude BOM Serial Number
        includeBomSerialNumber = false
        // Override component version
        componentVersion = "2.0.0"
    }
}

jacocoTestReport {
    reports {
        xml.required.set(true)
    }
}


test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.13"
    reportsDirectory = layout.buildDirectory.dir("$buildDir/reports/jacoco")
}

sonarqube {
    properties {
        property "sonar.java.binaries", "$buildDir/classes"
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}
